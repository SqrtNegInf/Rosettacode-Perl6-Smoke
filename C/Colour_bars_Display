#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Colour_bars/Display
#c# 2016-05-15 <RC, 2017-06-04 >RC, 2018-11-16 >RC
#t# graphical
#m# MOAR: OK
#j#  JVM: OK
#f# RC file: colorbars.ppm

my ($x,$y) = 1280, 720;
 
my @colors = map -> $r, $g, $b { Buf.new: |(($r, $g, $b) xx $x div 8) },
      0,   0,   0,
    255,   0,   0,
      0, 255,   0,
      0,   0, 255,
    255,   0, 255,
      0, 255, 255,
    255, 255,   0,
    255, 255, 255;
 
my $img = open "run/colorbars.ppm", :w orelse die "Can't create colorbars.ppm: $_";
 
$img.print: qq:to/EOH/;
    P6
    # colorbars.ppm
    $x $y
    255
    EOH
 
for ^$y {
    for ^@colors -> $h {
        $img.write: @colors[$h];
    }
}
 
$img.close;
