#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Card_shuffles
#c# 2016-05-22 <RC
#m# MOAR: OK
#j#  JVM: OK

srand 123456;

sub overhand ( @cards ) {
    my @splits = roll 10, ^( @cards.elems div 5 )+1;
    @cards.rotor( @splits  ,:partial ).reverse.flat
}

sub riffle ( @pile is copy ) {
    my @pile2 = @pile.splice: @pile.elems div 2 ;

    roundrobin(
        @pile.rotor(  (1 .. 3).roll(7), :partial ),
        @pile2.rotor( (1 .. 3).roll(9), :partial ),
    ).flat
}

my @cards = ^20;
@cards .= &overhand for ^10;
put @cards;

my @cards2 = ^20;
@cards2 .= &riffle for ^10;
put @cards2;

put my $cards3 = (^20).pick(*);

use Test;
if $*VM ~~ /jvm/ {
is @cards,  '10 3 0 2 9 4 5 1 6 7 16 19 8 18 11 14 13 15 12 17';
is @cards2, '0 11 4 3 16 17 13 2 6 1 5 14 12 15 19 18 8 9 7 10';
is $cards3, '7 8 11 16 15 4 10 17 3 13 9 0 14 1 6 5 19 2 18 12';
} else {
is @cards,  '2 5 0 1 4 9 12 3 13 10 6 8 11 15 14 16 7 17 19 18';
is @cards2, '0 3 8 14 18 12 13 17 5 11 10 19 15 4 9 16 6 2 1 7';
is $cards3, '15 4 11 10 17 13 19 8 16 14 9 5 1 0 18 6 12 2 7 3';
}
