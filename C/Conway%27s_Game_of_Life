#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Conway%27s_Game_of_Life
#c# 2016-08-01 <RC
#m# MOAR: OK
#j#  JVM: OK

class Automaton {
    subset World of Str where {
        .lines>>.chars.unique == 1 and m/^^<[.#\n]>+$$/
    }
    has Int ($.width, $.height);
    has @.a;

    multi method new (World $s) {
        self.new:
            :width(.pick.chars), :height(.elems),
            :a( .map: { [ .comb ] } )
                given $s.lines.cache;
    }

    method gist { join "\n", map { .join }, @!a }

    method C (Int $r, Int $c --> Bool) {
        @!a[$r % $!height][$c % $!width] eq '#';
    }
    method N (Int $r, Int $c --> Int) {
        +grep ?*, map { self.C: |@$_ },
            [ $r - 1, $c - 1], [ $r - 1, $c ], [ $r - 1, $c + 1],
            [ $r    , $c - 1],                 [ $r    , $c + 1],
            [ $r + 1, $c - 1], [ $r + 1, $c ], [ $r + 1, $c + 1];
    }

    method succ {
        self.new: :$!width, :$!height,
            :a(
                gather for ^$.height -> $r {
                    take [
                        gather for ^$.width -> $c {
                            take
                            (self.C($r, $c) == 1 && self.N($r, $c) == 2|3)
                                || (self.C($r, $c) == 0 && self.N($r, $c) == 3)
                            ?? '#' !! '.'
                        }
                    ]
                }
            )
    }
}

my Automaton $glider .= new: q:to/EOF/;
    ............
    ............
    ............
    .......###..
    .###...#....
    ........#...
    ............
    EOF


my @res;
for ^10 {
    @res.push: $glider++.gist;
    @res.push: '--';
}
.say for @res;

my $ref = qq:to/END/;
............
............
............
.......###..
.###...#....
........#...
............
--
............
............
........#...
..#....##...
..#....#.#..
..#.........
............
--
............
............
.......##...
.......#.#..
.###...#....
............
............
--
............
............
.......##...
..#...##....
..#.....#...
..#.........
............
--
............
............
......###...
......#.....
.###...#....
............
............
--
............
.......#....
......##....
..#...#.#...
..#.........
..#.........
............
--
............
......##....
......#.#...
......#.....
.###........
............
............
--
............
......##....
.....##.....
..#....#....
..#.........
..#.........
............
--
............
.....###....
.....#......
......#.....
.###........
............
............
--
......#.....
.....##.....
.....#.#....
..#.........
..#.........
..#.........
............
--
END

use Test;
is @res.join("\n"), chomp $ref;
