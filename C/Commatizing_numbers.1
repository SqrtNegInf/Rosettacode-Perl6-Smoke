#!/usr/bin/env perl6
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/Commatizing_numbers
#c# 2016-03-19 <RC
#m# MOAR: OK
#j#  JVM: OK

my @res;

commatize 'pi=3.14159265358979323846264338327950288419716939937510582097494459231', :at(6), :ins(' '), :by(5);
commatize 'The author has two Z 100000000000000 Zimbabwe notes (100 trillion).', :ins<.>;
commatize '-in AusD+1411.8millions';
commatize '===US 0017440 millions=== (in 2000 dollars)';
commatize '123.e8000 is pretty big.';
commatize 'The land area of the earth is  57268900(29% of the surface)  square miles.';
commatize 'Ain\'t no numbers in this here words, nohow, no way, Jose.';
commatize 'James was never known as  0000000007';
commatize 'Arthur Eddington wrote: I believe there are 15747724136275002577605653961181555468044717914527116709366231425076185631031296 protons in the universe.';
commatize '   -140000±100  millions.';
commatize '6/9/1946 was a good year for some.';

sub commatize($s, :$at = 0, :$ins = ',', :$by = 3) {
    my $temp = " After: "~ $s.subst:
        :continue($at), :1st,
        / <[1..9]> <[0..9]>* /,
        *.flip.comb(/<{ ".**1..$by" }>/).join($ins).flip;
    @res.push: $temp;
}
.say for @res;

my $ref = qq:to/END/;
 After: pi=3.14159 26535 89793 23846 26433 83279 50288 41971 69399 37510 58209 74944 59231
 After: The author has two Z 100.000.000.000.000 Zimbabwe notes (100 trillion).
 After: -in AusD+1,411.8millions
 After: ===US 0017,440 millions=== (in 2000 dollars)
 After: 123.e8000 is pretty big.
 After: The land area of the earth is  57,268,900(29% of the surface)  square miles.
 After: Ain't no numbers in this here words, nohow, no way, Jose.
 After: James was never known as  0000000007
 After: Arthur Eddington wrote: I believe there are 15,747,724,136,275,002,577,605,653,961,181,555,468,044,717,914,527,116,709,366,231,425,076,185,631,031,296 protons in the universe.
 After:    -140,000±100  millions.
 After: 6/9/1946 was a good year for some.
END

use Test;
is @res.join("\n"), chomp $ref;
