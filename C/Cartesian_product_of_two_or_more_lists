#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Cartesian_product_of_two_or_more_lists
#c# 2017-06-18 <RC
#m# MOAR: OK
#j#  JVM: OK

# Works with: Rakudo version 2017.06

# The cross meta operator X will return the cartesian product of two lists. To apply the cross meta-operator to a variable number of lists, use the reduce cross meta operator [X].

my @res;

# cartesian product of two lists using the X cross meta-operator
@res.push: ((1, 2) X (3, 4)).gist;
@res.push: ((3, 4) X (1, 2)).gist;
@res.push: ((1, 2) X ( )).gist;
@res.push: (( )    X ( 1, 2 )).gist;
 
# cartesian product of variable number of lists using
# the [X] reduce cross meta-operator
@res.push: ([X] (1776, 1789), (7, 12), (4, 14, 23), (0, 1)).gist;
@res.push: ([X] (1, 2, 3), (30), (500, 100)).gist;
@res.push: ([X] (1, 2, 3), (),   (500, 100)).gist;

.say for @res;

my $ref = qq:to/END/;
((1 3) (1 4) (2 3) (2 4))
((3 1) (3 2) (4 1) (4 2))
()
()
((1776 7 4 0) (1776 7 4 1) (1776 7 14 0) (1776 7 14 1) (1776 7 23 0) (1776 7 23 1) (1776 12 4 0) (1776 12 4 1) (1776 12 14 0) (1776 12 14 1) (1776 12 23 0) (1776 12 23 1) (1789 7 4 0) (1789 7 4 1) (1789 7 14 0) (1789 7 14 1) (1789 7 23 0) (1789 7 23 1) (1789 12 4 0) (1789 12 4 1) (1789 12 14 0) (1789 12 14 1) (1789 12 23 0) (1789 12 23 1))
((1 30 500) (1 30 100) (2 30 500) (2 30 100) (3 30 500) (3 30 100))
()
END

use Test;
is @res.join("\n"), chomp $ref;
