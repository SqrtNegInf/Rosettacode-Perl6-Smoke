#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/List_rooted_trees
#c# 2016-05-23 <RC
#m# MOAR: OK
#j#  JVM: OK

# Bags are represented by Perl 6 type Bag.

multi expand-tree ( Bag $tree ) {
    bag(bag(bag()) ⊎ $tree) ⊎
    [⊎] (
        $tree.keys ==> map {
            $^a.&expand-tree.map: * ⊎ ( $tree (-) bag($^a) )
        }
    );
}

multi expand-trees ( Bag $trees ) {
    [⊎] $trees.keys.map:  { $_.&expand-tree } ;
}      

my $n = 5;
my @res; my $line;
for ( bag(), bag(bag()), *.&expand-trees ... * )[$n] {
    #@res.push: ++$ ~ ". " ~ $_.gist;
    @res.push: $_.gist;
};

.put for @res.sort;

my $ref = q:to/END/;
Bag(Bag()(2), Bag(Bag())) => 2
Bag(Bag()(4)) => 1
Bag(Bag(), Bag(Bag()(2))) => 2
Bag(Bag(), Bag(Bag(Bag()))) => 2
Bag(Bag(Bag()(3))) => 1
Bag(Bag(Bag())(2)) => 1
Bag(Bag(Bag(), Bag(Bag()))) => 2
Bag(Bag(Bag(Bag()(2)))) => 1
Bag(Bag(Bag(Bag(Bag())))) => 1
END

use Test;
is @res.sort.join("\n"), chomp $ref;

# The Bag Bag(Bag(Bag()), Bag()(2)) coresponds with ((())()()). There are two independent ways how we can get it by nesting 4 Bags.

