#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Determine_if_only_one_instance_is_running
#c# 2016-03-07 <>RC, 2018-04-01 <RC
#t# skiptest
#m# MOAR: OK
#j#  JVM: OK
#n# 2018-04-01 Thundergnat undoes my changes, he's right to
#n# auto-testing doesn't address the real purpose of the routine

# Works with: rakudo version 2015-09-09
# An old-school Unix solution, none the worse for the wear:

my $name = $*PROGRAM-NAME;
my $pid = $*PID;

my $lockdir = "tmp";
my $lockfile = "$lockdir/$name.pid";
my $lockpid = "$lockfile$pid";
my $havelock = False;

END {
    unlink $lockfile if $havelock;
    try unlink $lockpid;
}

my $pidfile = open "$lockpid", :w or die "Can't create $lockpid: $!";
$pidfile.say($pid);
$pidfile.close;

if try link($lockpid, $lockfile) {
    $havelock = True;
    sleep 30;
}
else {
    shell "kill -CONT `cat $lockfile` || rm $lockfile";
    if try link($lockpid, $lockfile) {
        $havelock = True;
    }
    else {
        die "You can't run right now!";
    }
}
say "Got lock!";
unlink $lockpid;
