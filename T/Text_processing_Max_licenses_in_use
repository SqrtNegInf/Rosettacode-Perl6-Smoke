#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Text_processing/Max_licenses_in_use
#c# 2018-03-20 <RC
#m# MOAR: OK
#j# JVM:  OK

#Redirecting the mlijobs.txt file to STDIN:

my %licenses;
 
%licenses<count max> = 0,0;
 
#for $*IN.lines -> $line { 
for slurp("ref/mlijobs.txt").lines -> $line {
    my ( $license, $date_time );
    ( *, $license, *, $date_time ) = split /\s+/, $line;
    if $license eq 'OUT' {
        %licenses<count>++;
        if %licenses<count> > %licenses<max> {
            %licenses<max>   = %licenses<count>;
            %licenses<times> = [$date_time];
        }
        elsif %licenses<count> == %licenses<max> {
            %licenses<times>.push($date_time);
        }
    }
    else {
        if %licenses<count> == %licenses<max> {
            %licenses<times>[*-1] ~= " through " ~ $date_time;
        }
        %licenses<count>--;
    }
};
 
my $plural = %licenses<times>.elems == 1 ?? '' !! 's';
 
my @res.push: "Maximum concurrent licenses in use: {%licenses<max>}, in the time period{$plural}:";
   @res.push: join ",\n", %licenses<times>.list;
.say for @res;

my $ref = qq:to/END/;
Maximum concurrent licenses in use: 99, in the time periods:
2008/10/03_08:39:34 through 2008/10/03_08:39:45,
2008/10/03_08:40:40 through 2008/10/03_08:40:47
END

use Test;
is @res.join("\n"), chomp $ref;
