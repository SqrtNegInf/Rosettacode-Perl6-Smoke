#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Text_processing/1
#c# 2018-03-15 <RC
#m# MOAR: OK
#j# JVM:  OK

my @gaps;
my $previous = 'valid';

my @res;

#for $*IN.lines -> $line { 
for slurp("ref/text-proc.txt").lines -> $line {
    my ($date, @readings) = split /\s+/, $line;
    my @valid;
    my $hour = 0;
    for @readings -> $reading, $flag {
        if $flag > 0 {    
            @valid.push($reading);
            if $previous eq 'invalid' {
                @gaps[*-1]{'end'} = "$date $hour:00";
                $previous = 'valid';
            }
        } 
        else
        {
            if $previous eq 'valid' {
                @gaps.push( {start => "$date $hour:00"} );
            }
            @gaps[*-1]{'count'}++;
            $previous = 'invalid';
        }
        $hour++;
    }
    @res.push: "$date: { ( +@valid ?? ( ( [+] @valid ) / +@valid ).fmt("%.3f") !! 0 ).fmt("%8s") }"~
        " mean from { (+@valid).fmt("%2s") } valid."; 
};

my $longest = @gaps.sort({-$^a<count>})[0];

@res.push: "Longest period of invalid readings was {$longest<count>} hours, "~
    "from {$longest<start>} till {$longest<end>}.";

#.say for @res;
say @res[*-1];

my $ref = 'Longest period of invalid readings was 589 hours, from 1993-02-09 1:00 till 1993-03-05 14:00.';

use Test;
is @res[*-1], chomp $ref;
