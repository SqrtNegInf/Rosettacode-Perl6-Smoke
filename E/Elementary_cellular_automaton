#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Elementary_cellular_automaton
#c# 2016-08-19 <RC
#m# MOAR: OK
#j#  JVM: OK

class Automaton {
    has $.rule;
    has @.cells;
    has @.code = $!rule.fmt('%08b').flip.comb».Int;

    method gist { "|{ @!cells.map({$_ ?? '#' !! ' '}).join }|" }

    method succ {
        self.new: :$!rule, :@!code, :cells(

            @!code[
                    4 «×« @!cells.rotate(-1)
                »+« 2 «×« @!cells
                »+«       @!cells.rotate(1)
            ]
        )
    }
}

my $size = 10;

my Automaton $a .= new:
    :rule(30),
    :cells( flat 0 xx $size, 1, 0 xx $size );

my @res.push: $a++.gist for ^$size;
.say for @res;

my $ref = qq:to/END/;
|          #          |
|         ###         |
|        ##  #        |
|       ## ####       |
|      ##  #   #      |
|     ## #### ###     |
|    ##  #    #  #    |
|   ## ####  ######   |
|  ##  #   ###     #  |
| ## #### ##  #   ### |
END

use Test;
is @res.join("\n"), chomp $ref;
