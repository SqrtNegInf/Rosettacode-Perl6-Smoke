#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Cheryl%27s_Birthday
#c# 2018-10-22 <RC
#m# MOAR: OK
#j# JVM:  OK

my @dates = 
    { :15day, :5month },
    { :16day, :5month },
    { :19day, :5month },
    { :17day, :6month },
    { :18day, :6month },
    { :14day, :7month },
    { :16day, :7month },
    { :14day, :8month },
    { :15day, :8month },
    { :17day, :8month }
;
 
# Month can't have a unique day
my @filtered = @dates.grep(*.<month> != one(@dates.grep(*.<day> == one(@dates».<day>))».<month>));
 
# Day must be unique and unambiguous in remaining months
my $birthday = @filtered.grep(*.<day> == one(@filtered».<day>)).classify({.<month>})\
    .first(*.value.elems == 1).value[0];
 
# convenience hash
my %months = <'' January February March April May June July
      August September October November December>.kv.pairup;

say my $result = "Cheryl's birthday is { %months{$birthday<month>} } {$birthday<day>}.";

use Test;
is $result, qq{Cheryl's birthday is July 16.};
