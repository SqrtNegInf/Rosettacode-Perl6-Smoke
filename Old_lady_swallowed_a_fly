#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Old_lady_swallowed_a_fly
#c# 2016-06-05 <>RC
#m# MOAR: OK
#j#  JVM: OK

my @victims =
    fly    => "  I don't know why S—",
    spider => "  That wriggled and jiggled and tickled inside her.",
    bird   => "  How absurd, T!",
    cat    => "  Fancy that, S!",
    dog    => "  What a hog, T!",
    goat   => "  She just opened her throat, and in walked the goat!",
    cow    => "  I don't know how S!",
    horse  => "  She's dead, of course...";
 
my @history = "I guess she'll die...\n";
 
my @res;
for (flat @victims».kv) -> $victim, $_ is copy {
    @res.push: "There was an old lady who swallowed a $victim...";
 
    s/ «S» /she swallowed the $victim/;
    s/ «T» /to swallow a $victim!/;
    @res.push: $_;
    last when /dead/;
 
    @history[0] ~~ s/^X/She swallowed the $victim/;
    @res.push: $_ for @history;
    @history.unshift($_) if @history < 5;
    @history.unshift("X to catch the $victim,");
}

.say for @res;

# And that's how I larned it!
my $ref = qq:to/END/;
There was an old lady who swallowed a fly...
  I don't know why she swallowed the fly—
I guess she'll die...

There was an old lady who swallowed a spider...
  That wriggled and jiggled and tickled inside her.
She swallowed the spider to catch the fly,
  I don't know why she swallowed the fly—
I guess she'll die...

There was an old lady who swallowed a bird...
  How absurd, to swallow a bird!!
She swallowed the bird to catch the spider,
  That wriggled and jiggled and tickled inside her.
She swallowed the spider to catch the fly,
  I don't know why she swallowed the fly—
I guess she'll die...

There was an old lady who swallowed a cat...
  Fancy that, she swallowed the cat!
She swallowed the cat to catch the bird,
She swallowed the bird to catch the spider,
  That wriggled and jiggled and tickled inside her.
She swallowed the spider to catch the fly,
  I don't know why she swallowed the fly—
I guess she'll die...

There was an old lady who swallowed a dog...
  What a hog, to swallow a dog!!
She swallowed the dog to catch the cat,
She swallowed the cat to catch the bird,
She swallowed the bird to catch the spider,
  That wriggled and jiggled and tickled inside her.
She swallowed the spider to catch the fly,
  I don't know why she swallowed the fly—
I guess she'll die...

There was an old lady who swallowed a goat...
  She just opened her throat, and in walked the goat!
She swallowed the goat to catch the dog,
She swallowed the dog to catch the cat,
She swallowed the cat to catch the bird,
She swallowed the bird to catch the spider,
  That wriggled and jiggled and tickled inside her.
She swallowed the spider to catch the fly,
  I don't know why she swallowed the fly—
I guess she'll die...

There was an old lady who swallowed a cow...
  I don't know how she swallowed the cow!
She swallowed the cow to catch the goat,
She swallowed the goat to catch the dog,
She swallowed the dog to catch the cat,
She swallowed the cat to catch the bird,
She swallowed the bird to catch the spider,
  That wriggled and jiggled and tickled inside her.
She swallowed the spider to catch the fly,
  I don't know why she swallowed the fly—
I guess she'll die...

There was an old lady who swallowed a horse...
  She's dead, of course...
END

use Test;
is @res.join("\n"), chomp $ref;
