#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Echo_server
#c# 2016-04-09 <>RC, 2018-04-01 <RC, 2018-04-18 <RC
#t# interactive
#m# MOAR: OK
#j# JVM:  OK
#n# either/or, comment one out to test the other

my $socket = IO::Socket::INET.new:
    :localhost<localhost>,
    :localport<12321>,
    :listen;
 
while $socket.accept -> $conn {
    say "Accepted connection";
    start {
        while $conn.recv -> $stuff {
            say "Echoing $stuff";
            $conn.print($stuff);
        }
        $conn.close;
    }
}

# Async version:

react {
    whenever IO::Socket::Async.listen('0.0.0.0', 12321) -> $conn {
        whenever $conn.Supply.lines -> $line {
            $conn.print( $line ) ;
        }
    }
}
