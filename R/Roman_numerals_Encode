#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Roman_numerals/Encode
#c# 2015-10-13 <RC
#m# MOAR: OK
#j#  JVM: OK

my %symbols =
    1 => "I", 5 => "V", 10 => "X", 50 => "L", 100 => "C", 500 => "D", 1_000 => "M";

my @subtractors =
    1_000, 100,  500, 100,  100, 10,  50, 10,  10, 1,  5, 1,  1, 0;

multi sub roman (0) { '' }
multi sub roman (Int $n) {
    for @subtractors -> $cut, $minus {
        $n >= $cut
            and return %symbols{$cut} ~ roman($n - $cut);
        $n >= $cut - $minus
            and return %symbols{$minus} ~ roman($n + $minus);
     }
}

say roman($_) for <1990 2008 1666>;
#for 1 .. 2_010 -> $x { say roman($x); }

use Test;
is roman(1990), 'MCMXC', '1990 => MCMXC';
is roman(2008), 'MMVIII', '2008 => MMVIII';
is roman(1666), 'MDCLXVI', '1666 => MDCLXVI';

