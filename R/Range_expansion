#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Range_expansion
#c# 2016-08-05 <RC
#m# MOAR: OK
#j#  JVM: OK

# Works with: Rakudo version 2016.07

sub range-expand (Str $range-description) {
    my token number { '-'? \d+ }
    my token range  { (<&number>) '-' (<&number>) }

    $range-description
        .split(',')
        .map({ .match(&range) ?? $0..$1 !! +$_ })
        .flat
}

say my $result1 = range-expand('-6,-3--1,3-5,7-11,14,15,17-20');

use Test;
is $result1, '-6 -3 -2 -1 3 4 5 7 8 9 10 11 14 15 17 18 19 20';

# Alternatively, using a grammar:

grammar RangeList {
    token TOP    { <term>* % ','    { make $<term>.map(*.made)       } }
    token term   { [<range>|<num>]  { make ($<num> // $<range>).made } }
    token range  { <num> '-' <num>  { make +$<num>[0] .. +$<num>[1]  } }
    token num    { '-'? \d+         { make +$/                       } }
}

say my $result2 = RangeList.parse('-6,-3--1,3-5,7-11,14,15,17-20').made.flat.join(', ');
is $result2, '-6, -3, -2, -1, 3, 4, 5, 7, 8, 9, 10, 11, 14, 15, 17, 18, 19, 20';
