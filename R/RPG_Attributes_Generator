#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/RPG_Attributes_Generator
#c# 2018-07-20 <RC
#m# MOAR: OK
#j# JVM:  OK

srand 123456;

# Works with: Rakudo Star version 2018.04.1

my ( $min_sum, $hero_attr_min, $hero_count_min ) = 75, 15, 2;
my @attr-names = <Str Int Wis Dex Con Cha>;

sub heroic { + @^a.grep: * >= $hero_attr_min }

my @attr;
repeat until @attr.sum     >= $min_sum
         and heroic(@attr) >= $hero_count_min {

    @attr = @attr-names.map: { (1..6).roll(4).sort(+*).skip(1).sum };
}

my @res = (@attr-names Z=> @attr).gist;
@res.push: "Sum: {@attr.sum}, with {heroic(@attr)} attributes >= $hero_attr_min";

.say for @res;

my $moar = qq:to/END/;
(Str => 16 Int => 14 Wis => 14 Dex => 8 Con => 16 Cha => 13)
Sum: 81, with 2 attributes >= 15
END

my $jvm = qq:to/END/;
(Str => 16 Int => 13 Wis => 10 Dex => 8 Con => 15 Cha => 14)
Sum: 76, with 2 attributes >= 15
END

use Test;
my $ref = $*VM ~~ /jvm/ ?? $jvm !! $moar;
is @res.join("\n"), chomp $ref;
