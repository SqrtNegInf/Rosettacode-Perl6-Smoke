#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Reduced_row_echelon_form
#c# 2015-12-18 <RC
#m# MOAR: OK
#j#  JVM: OK

# And the same code, recast into OO. Also, scale and shear are recast as unscale and unshear, 
# which fit the problem better.

class Matrix is Array {
    method unscale_row ( @M: $scale, $row ) {
        @M[$row] = @M[$row] »/» $scale;
    }
    method unshear_row ( @M: $scale, $r1, $r2 ) {
        @M[$r1] = @M[$r1] »-» ( @M[$r2] »×» $scale );
    }
    method reduce_row ( @M: $row, $col ) {
        @M.unscale_row( @M[$row][$col], $row );
    }
    method clear_column ( @M: $row, $col ) {
        for @M.keys.grep( * != $row ) -> $scanning_row {
            @M.unshear_row( @M[$scanning_row][$col], $scanning_row, $row );
        }
    }
    method reduced_row_echelon_form ( @M: ) {
        my $column_count = +@( @M[0] );

        my $current_col = 0;
        # Skip past all-zero columns.
        while all( @M».[$current_col] ) == 0 {
            $current_col++;
            return if $current_col == $column_count; # Matrix was all-zeros.
        }

        for @M.keys -> $current_row {
            @M.reduce_row(   $current_row, $current_col );
            @M.clear_column( $current_row, $current_col );
            $current_col++;
            return if $current_col == $column_count;
        }
    }
}

my $M = Matrix.new(
    [<  1   2   -1    -4 >],
    [<  2   3   -1   -11 >],
    [< -2   0   -3    22 >],
);

$M.reduced_row_echelon_form;

say @($_)».fmt('%4g') for @($M);

# Note that both versions can be simplified using Z+=, Z-=, X*=, and X/= to scale and shear.

#`{
Reduced Row Echelon Form Matrix
     1      0      0     -8
     0      1      0      1
     0      0      1     -2
}
use Test;
is @$M, '1 0 0 -8 0 1 0 1 0 0 1 -2';
