#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Damm_algorithm
#c# 2017-05-30 <RC
#m# MOAR: OK
#j#  JVM: OK

# Works with: Rakudo version 2017.05
sub damm ( *@digits ) {
    my @tbl = [0, 3, 1, 7, 5, 9, 8, 6, 4, 2],
              [7, 0, 9, 2, 1, 5, 4, 8, 6, 3],
              [4, 2, 0, 6, 8, 7, 1, 3, 5, 9],
              [1, 7, 5, 0, 9, 8, 3, 4, 2, 6],
              [6, 1, 2, 3, 0, 4, 5, 9, 7, 8],
              [3, 6, 7, 4, 2, 0, 9, 5, 8, 1],
              [5, 8, 6, 9, 7, 2, 0, 1, 3, 4],
              [8, 9, 4, 5, 3, 6, 2, 0, 1, 7],
              [9, 4, 3, 8, 6, 1, 7, 2, 0, 5],
              [2, 5, 8, 1, 4, 3, 6, 7, 9, 0];
    my $row = 0;
    for @digits -> $col { $row = @tbl[$row][$col] }
    not $row
}
 
my @res;
for 5724, 5727, 112946 {
    @res.push: "$_:\tChecksum digit { damm( $_.comb ) ?? '' !! 'in' }correct."
}
.say for @res;

my $ref = qq:to/END/;
5724:	Checksum digit correct.
5727:	Checksum digit incorrect.
112946:	Checksum digit correct.
END

use Test;
#my $ref = $*VM ~~ /jvm/ ?? $jvm !! $moar;
is @res.join("\n"), chomp $ref;
