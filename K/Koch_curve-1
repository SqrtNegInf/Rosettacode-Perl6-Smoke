#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Koch_curve
#c# 2018-04-24 <RC
#t# graphical
#m# MOAR: OK
#j# JVM:  OK
#f# RC file: koch_curve-1.svg

# Works with: Rakudo version 2018.03

# Koch curve, actually a full Koch snowflake.

use SVG;

my $fh = open("run/koch_curve-1.svg", :w) || die;

role Lindenmayer {
    has %.rules;
    method succ {
        self.comb.map( { %!rules{$^c} // $c } ).join but Lindenmayer(%!rules)
    }
}

my $flake = 'F--F--F' but Lindenmayer( { F => 'F+F--F+F' } );

$flake++ xx 5;
my @points = (50, 440);

for $flake.comb -> $v {
    state ($x, $y) = @points[0,1];
    state $d = 2 + 0i;
    with $v {
        when 'F' { @points.append: ($x += $d.re).round(.01), ($y += $d.im).round(.01) }
        when '+' { $d ×= .5 + .8660254i }
        when '-' { $d ×= .5 - .8660254i }
    }
}

$fh.say: SVG.serialize(
    svg => [
        width => 600, height => 600, style => 'stroke:rgb(0,0,255)',
        :rect[:width<100%>, :height<100%>, :fill<white>],
        :polyline[ points => @points.join(','), :fill<white> ],
    ],
);
