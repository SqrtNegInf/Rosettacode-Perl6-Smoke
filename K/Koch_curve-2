#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Koch_curve
#c# 2018-04-24 <RC
#t# graphical
#m# MOAR: OK
#j# JVM:  OK
#f# RC file: koch_curve-2.svg

# Variation using 90° angles:

use SVG;

my $fh = open("run/koch_curve-2.svg", :w) || die;

role Lindenmayer {
    has %.rules;
    method succ {
	    self.comb.map( { %!rules{$^c} // $c } ).join but Lindenmayer(%!rules)
    }
}

my $koch = 'F' but Lindenmayer( { F => 'F+F-F-F+F', } );

$koch++ xx 4;
my @points = (450, 250);

for $koch.comb -> $v {
    state ($x, $y) = @points[0,1];
    state $d = -5 - 0i;
    with $v {
        when 'F' { @points.append: ($x += $d.re).round(.01), ($y += $d.im).round(.01) }
        when /< + - >/ { $d ×= "{$v}1i" }
    }
}

$fh.say: SVG.serialize(
    svg => [
        width => 500, height => 300, style => 'stroke:rgb(0,0,255)',
        :rect[:width<100%>, :height<100%>, :fill<white>],
        :polyline[ points => @points.join(','), :fill<white> ],
    ],
);
