#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Chebyshev_coefficients
#c# 2016-03-07 <RC
#m# MOAR: OK
#j#  JVM: OK

# Works with: Rakudo version 2015.12
#Translation of: C

sub chebft ( Code $func, Real $a, Real $b, Int $n ) {

    my $bma = 0.5 * ( $b - $a );
    my $bpa = 0.5 * ( $b + $a );

    my @pi_n = ( (^$n).list »+» 0.5 ) »*» ( pi / $n );
    my @f    = ( @pi_n».cos »*» $bma »+» $bpa )».$func;
    my @sums = map { [+] @f »*« ( @pi_n »*» $_ )».cos }, ^$n;

    return @sums »*» ( 2 / $n );
}

my @res.push: .fmt('%+13.7e') for chebft &cos, 0, 1, 10;
.say for @res;

my $moar = qq:to/END/;
+1.6471695e+00
-2.3229937e-01
-5.3715115e-02
+2.4582353e-03
+2.8211906e-04
-7.7222292e-06
-5.8985565e-07
+1.1521427e-08
+6.5962992e-10
-1.0021994e-11
END

my $jvm = qq:to/END/;
+1.6471695e+00
-2.3229937e-01
-5.3715115e-02
+2.4582353e-03
+2.8211906e-04
-7.7222292e-06
-5.8985565e-07
+1.1521428e-08
+6.5962992e-10
-1.0022039e-11
END

use Test;
if $*VM ~~ /jvm/ {
is @res.join("\n"), chomp $jvm;
} else {
is @res.join("\n"), chomp $moar;
}
