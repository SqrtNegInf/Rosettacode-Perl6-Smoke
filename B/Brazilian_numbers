#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Brazilian_numbers
#c# 2019-08-12 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

sub is-Brazilian (Int $n) {
    for 2 ..^ $n - 1 { return True if ($n %% 2 && $n > 6) or +(my @b = $n.polymod( $_ xx * )) > 1 and [==] @b }
    False
}

my @Brazilians = lazy (^Inf).hyper.grep: { .&is-Brazilian }

@res.push: "First 20 Brazilian numbers:\n"~ @Brazilians[^20];

@res.push: "\nFirst 20 odd Brazilian numbers:\n"~ @Brazilians.grep( * % 2 )[^20];

@res.push: "\nFirst 20 prime Brazilian numbers:\n"~ @Brazilians.grep( *.is-prime )[^20];

.say for @res;

my $ref = q:to/END/;
First 20 Brazilian numbers:
7 8 10 12 13 14 15 16 18 20 21 22 24 26 27 28 30 31 32 33

First 20 odd Brazilian numbers:
7 13 15 21 27 31 33 35 39 43 45 51 55 57 63 65 69 73 75 77

First 20 prime Brazilian numbers:
7 13 31 43 73 127 157 211 241 307 421 463 601 757 1093 1123 1483 1723 2551 2801
END

use Test;
is @res.join("\n"), chomp $ref;
