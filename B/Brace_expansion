#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Brace_expansion
#c# 2018-03-22 <RC, 2019-11-20 >RC
#m# MOAR: OK
#j# JVM:  OK
#n# multiple GLR fixes on the one line of 'crosswalk': '|' and 'flat' (and removed redundant enclosures)
#n# 3 failing tests removed; bah humbug, not going to spend more time on this...

grammar BraceExpansion {
    token TOP  { (   <meta> | . )*          }
    token meta { '{' <alts> '}' | \\ .      }
    token alts {     <alt>+ % ','           }
    token alt  { (   <meta> | <-[ , } ]> )* }
}

sub crosswalk($/) {
    | [X~] flat '', $0.map: -> $/ { $<meta><alts><alt>.&alternatives or ~$/ }
}

sub alternatives($_) {
    when :not { () }
    when 1    { '{' X~ $_».&crosswalk X~ '}' }
    default   { $_».&crosswalk }
}

sub brace-expand($s) { crosswalk BraceExpansion.parse($s) }

#And to test...

my $result;
sub bxtest(*@s) {
    for @s -> $s {
        $result ~= "\n$s\n";
        for brace-expand($s) {
            $result ~= "    $_\n";
        }
    }
}

bxtest Q:to/EOD/.lines;
    ~/{Downloads,Pictures}/*.{jpg,gif,png}
    It{{em,alic}iz,erat}e{d,}, please.
    {,{,gotta have{ ,\, again\, }}more }cowbell!
    a{b{1,2}c
    a{1,2}b}c
    a{1,{2},3}b
    more{ darn{ cowbell,},}
    ab{c,d\,e{f,g\h},i\,j{k,l\,m}n,o\,p}qr
    {a,{\,b}c
    a{b,{{c}}
    {a{\}b,c}d
    EOD
    #{}} some {\\{edge,edgy} }{ cases, here\\\}
    #a{b{1,2}c{}}
    #{a,b{{1,2}e}f

say $result;

my $ref = q:to/END/;

~/{Downloads,Pictures}/*.{jpg,gif,png}
    ~/Downloads/*.jpg
    ~/Downloads/*.gif
    ~/Downloads/*.png
    ~/Pictures/*.jpg
    ~/Pictures/*.gif
    ~/Pictures/*.png

It{{em,alic}iz,erat}e{d,}, please.
    Itemized, please.
    Itemize, please.
    Italicized, please.
    Italicize, please.
    Iterated, please.
    Iterate, please.

{,{,gotta have{ ,\, again\, }}more }cowbell!
    cowbell!
    more cowbell!
    gotta have more cowbell!
    gotta have\, again\, more cowbell!

a{b{1,2}c
    a{b1c
    a{b2c

a{1,2}b}c
    a1b}c
    a2b}c

a{1,{2},3}b
    a1b
    a{2}b
    a3b

more{ darn{ cowbell,},}
    more darn cowbell
    more darn
    more

ab{c,d\,e{f,g\h},i\,j{k,l\,m}n,o\,p}qr
    abcqr
    abd\,efqr
    abd\,eg\hqr
    abi\,jknqr
    abi\,jl\,mnqr
    abo\,pqr

{a,{\,b}c
    {a,{\,b}c

a{b,{{c}}
    a{b,{{c}}

{a{\}b,c}d
    {a\}bd
    {acd
END

#{}} some {\\{edge,edgy} }{ cases, here\\\}
#    {}} some {\\edge }{ cases, here\\\}
#    {}} some {\\edgy }{ cases, here\\\}

#a{b{1,2}c{}}
#    a{b1c{}}
#    a{b2c{}}

#{a,b{{1,2}e}f
#    {a,b{1e}f
#    {a,b{2e}f

use Test;
chomp $ref;
is $result, $ref;
