#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Bioinformatics/Subsequence
#c# 2021-03-22 <RC
#m# MOAR: OK
#j# JVM:  OK

srand 123;

my @res;

my $line = 80;

my $haystack = [~] <A C G T>.roll($line * 8);

@res.push: 'Needle: ' ~ my $needle = [~] <A C G T>.roll(4);

my $these = $haystack ~~ m:g/<$needle>/;

my @match = $these.map: { .from, .pos }

@res.push: sprintf "From: %3s to %3s", |$_ for @match;

say my $result = join "\n", @res;

my $ref = q:to/END/;
Needle: AACG
From: 250 to 254
From: 536 to 540
From: 547 to 551
From: 611 to 615
END

use Test;
is @res.join("\n"), chomp $ref;
