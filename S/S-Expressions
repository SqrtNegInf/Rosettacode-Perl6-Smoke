#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/S-Expressions
#c# 2015-11-02 <RC
#m# MOAR: OK
#j#  JVM: OK

# Works with: Rakudo version version 2014.04-250-gea173d0 built on MoarVM
# version 2014.04-98-gbed1693 #This parses the task, but it isn't really a
# good lisp parser, because it always wants whitespace between lists, so
# (()()) will fail ( (() ()) wont)

grammar S-Exp {
  rule TOP    {^ <s-list> $};

  token s-list { '(' ~ ')' [ <in_list>+ % [\s+] | '' ] }
  token in_list { <s-token> | <s-list> } 

  proto token s-token {*}
  token s-token:sym<Num>    {\d*\.?\d+}
  token s-token:sym<String> {'"' ['\"' |<-[\\"]>]*? '"'} #'
  token s-token:sym<Atom>   {<-[()\s]>+}

}

# The Actions class, for each syntactic rule there is a method
# that stores some data in the abstract syntax tree with make
class S-Exp::ACTIONS {
  method TOP ($/) {make $<s-list>.ast}
  method s-list ($/) {make [$<in_list>Â».ast]}
  method in_list ($/) {make $/.values[0].ast}

  method s-token:sym<Num> ($/){make +$/}
  method s-token:sym<String> ($/){make ~$/.substr(1,*-1)}
  method s-token:sym<Atom> ($/){make ~$/}
}

multi s-exp_writer (Positional $ary) {'(' ~ $ary.map(&s-exp_writer).join(' ') ~ ')'}
multi s-exp_writer (Numeric    $num) {~$num}
multi s-exp_writer (Str        $str) {
  return $str unless $str ~~ /<[(")]>|\s/;
  return '()' if $str eq '()';
  '"' ~ $str.subst('"', '\"' ) ~ '"';
}


my $s-exp = '((data "quoted data" 123 4.5)
 (data (!@# (4.5) "(more" "data)")))';

my $actions = S-Exp::ACTIONS.new();
my $perl_array = (S-Exp.parse($s-exp, :$actions)).ast;

say my $r1 = "the expression:\n$s-exp\n";
say my $r2 = "the perl6-expression:\n{$perl_array.raku}\n";
say my $r3 = "and back:\n{s-exp_writer($perl_array)}\n";

my $ref1 = qq:to/END/;
the expression:
((data "quoted data" 123 4.5)
 (data (!@# (4.5) "(more" "data)")))
END

my $ref2 = qq:to/END/;
the perl6-expression:
[["data", "quoted data", "123", 4.5], ["data", ["!\\@#", [4.5], "(more", "data)"]]]
END

my $ref3 = qq:to/END/;
and back:
((data "quoted data" 123 4.5) (data (!@# (4.5) "(more" "data)")))
END

use Test;
is $r1, $ref1;
is ($r2.subst(/\$\[\[/, '[[')), $ref2;
is $r3, $ref3;
