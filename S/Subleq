#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Subleq
#c# 2021-05-29 <RC
#m# MOAR: OK
#j# JVM:  OK

my $result;

my @hello-world = 
|<15 17 -1 17 -1 -1 16 1 -1 16 3 -1 15 15 0 0 -1>,
|"Hello, world!\n\0".comb.map(*.ord);

sub run-subleq(@memory) {
  my $ip = 0;
  while $ip >= 0 && $ip < @memory {
    my ($a, $b, $c) = @memory[$ip..*];
    $ip += 3;
    if $a < 0 {
      @memory[$b] = getc.ord;
    } elsif $b < 0 {
      $result ~= @memory[$a].chr;
    } else {
      if (@memory[$b] -= @memory[$a]) <= 0 {
        $ip = $c;
      } 
    }
  }
}

run-subleq @hello-world;

use Test;
is $result, "Hello, world!\n";
