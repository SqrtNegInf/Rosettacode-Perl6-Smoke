#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Sudoku
#c# 2015-10-14 <RC
#m# MOAR: OK
#j#  JVM: OK

use v6;
my @A = <
    5 3 0  0 2 4  7 0 0 
    0 0 2  0 0 0  8 0 0 
    1 0 0  7 0 3  9 0 2 

    0 0 8  0 7 2  0 4 9 
    0 2 0  9 8 0  0 7 0 
    7 9 0  0 0 0  0 8 0 

    0 0 0  0 3 0  5 0 6 
    9 6 0  0 1 0  3 0 0 
    0 5 0  6 9 0  0 1 0
>;

my &I = * div 9;  # line number
my &J = * % 9;    # column number
my &K = { ($_ div 27) Ã— 3 + $_ % 9 div 3 }; # bloc number

my @res;

sub solve {
    for ^@A -> $i {
    next if @A[$i];
    my @taken-values = @A[
        grep {
        I($_) == I($i) || J($_) == J($i) || K($_) == K($i)
        }, ^@A
    ];
    for grep none(@taken-values), 1..9 {
        @A[$i] = $_;
        solve;
    }
    return @A[$i] = 0;
    }
    my $i = 1;
    my $line;
    for ^@A {
    $line ~= "@A[$_] ";
    $line ~= " "     if $i   %%  3;
    @res.push: $line if $i   %%  9;
    $line = ''       if $i   %%  9;
    @res.push: ''    if $i++ %% 27;
    }
}

solve;
.say for @res;

my $ref = qq:to/END/;
5 3 9  8 2 4  7 6 1  
6 7 2  1 5 9  8 3 4  
1 8 4  7 6 3  9 5 2  

3 1 8  5 7 2  6 4 9  
4 2 5  9 8 6  1 7 3  
7 9 6  3 4 1  2 8 5  

8 4 1  2 3 7  5 9 6  
9 6 7  4 1 5  3 2 8  
2 5 3  6 9 8  4 1 7  

END

use Test;
is @res.join("\n"), chomp $ref
