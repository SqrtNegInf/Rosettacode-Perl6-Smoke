#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/The_Twelve_Days_of_Christmas
#c# 2015-12-17 <RC
#m# MOAR: OK
#j#  JVM: OK
#f# RC file: the_twelve_days.txt

my $fh = open('run/the_twelve_days.txt', :w);

my @days = <first second third fourth fifth sixth seventh eighth ninth tenth eleventh twelfth>;

my @gifts = lines q:to/END/;
  And a partridge in a pear tree.
  Two turtle doves,
  Three french hens,
  Four calling birds,
  Five golden rings,
  Six geese a-laying,
  Seven swans a-swimming,
  Eight maids a-milking,
  Nine ladies dancing,
  Ten lords a-leaping,
  Eleven pipers piping,
  Twelve drummers drumming,
END

sub nth($n) { $fh.say: "On the @days[$n] day of Christmas, my true love gave to me:" }

nth(0);
$fh.say: @gifts[0].subst('And a','A');

for 1 ... 11 -> $d {
    $fh.say: '';
    nth($d);
    $fh.say: @gifts[$_] for $d ... 0;
}
