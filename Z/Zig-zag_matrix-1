#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Zig-zag_matrix
#c# 2016-01-18 <>RC
#m# MOAR: OK
#j#  JVM: OK

class Turtle {
    my @dv = [0,-1], [1,-1], [1,0], [1,1], [0,1], [-1,1], [-1,0], [-1,-1];
    my $points = 8; # 'compass' points of neighbors on grid: north=0, northeast=1, east=2, etc.

    has @.loc = 0,0;
    has $.dir = 0;
    has %.world;
    has $.maxegg;
    has $.range-x;
    has $.range-y;

    method turn-left ($angle = 90) { $!dir -= $angle / 45; $!dir %= $points; }
    method turn-right($angle = 90) { $!dir += $angle / 45; $!dir %= $points; }

    method lay-egg($egg) {
    %!world{~@!loc} = $egg;
    $!maxegg max= $egg;
    $!range-x minmax= @!loc[0];
    $!range-y minmax= @!loc[1];
    }

    method look($ahead = 1) {
    my $there = @!loc »+« @dv[$!dir] »×» $ahead;
    %!world{~$there};
    }

    method forward($ahead = 1) {
    my $there = @!loc »+« @dv[$!dir] »×» $ahead;
    @!loc = @($there);
    }

    method showmap() {
    gather {
    my $form = "%{$!maxegg.chars}s";
    my $endx = $!range-x.max;
        for $!range-y.list X $!range-x.list -> ($y, $x) {
        take sprintf (%!world{"$x $y"} // '').fmt($form);
        take sprintf $x == $endx ?? "\n" !! ' ';
    }
    }
    }
}

#Assuming the same Turtle class that is used in Spiral_matrix:

my $size = 5;
my $t = Turtle.new(dir => 1);
my $counter = 0;
for 1 ..^ $size -> $run {
	for ^$run {
	    $t.lay-egg($counter++);
	    $t.forward;
	}
	my $yaw = $run %% 2 ?? -1 !! 1;
	$t.turn-right($yaw × 135); $t.forward; $t.turn-right($yaw × 45);
    }
for $size ... 1 -> $run {
	for ^$run -> $ {
	    $t.lay-egg($counter++);
	    $t.forward;
	}
	$t.turn-left(180); $t.forward;
	my $yaw = $run %% 2 ?? 1 !! -1;
	$t.turn-right($yaw × 45); $t.forward; $t.turn-left($yaw × 45);
    }
put ' ' ~ $t.showmap;

my $ref = qq:to/END/;
( 0    1    5    6   14 
  2    4    7   13   15 
  3    8   12   16   21 
  9   11   17   20   22 
 10   18   19   23   24 
)
END

use Test;
is $t.showmap.gist, chomp $ref;
