#!/usr/bin/env perl6
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/Prime_decomposition
#c# 2015-11-03 <RC
#m# MOAR: OK
#j#  JVM: OK

constant @primes = 2, 3, 5, -> *@p {
    my $n = @p[*-1];
    repeat { $n += 2 } while $n %% any @p.grep: * **2 <= $n;
    $n;
} ... *;

sub factors(Int $remainder is copy) {
  return 1 if $remainder <= 1;
  gather for @primes -> $factor {
    if $factor * $factor > $remainder {
      take $remainder if $remainder > 1;
      last;
    }

    # How many times can we divide by this prime?
    while $remainder %% $factor {
        take $factor;
        last if ($remainder div= $factor) === 1;
    }
  }
}

say factors 100547;
say factors +'100547'.flip;
#say factors 536870911;

use Test;
is (factors 536870911), <233 1103 2089>, 'I could have done this faster in my head...';
