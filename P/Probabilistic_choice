#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Probabilistic_choice
#c# 2016-05-08 <RC
#m# MOAR: OK
#j#  JVM: OK

srand 123456;

# Works with: rakudo version 2015-10-20
constant TRIALS = 1e6;

constant @event = <aleph beth gimel daleth he waw zayin heth>;

constant @P = flat (1 X/ 5 .. 11), 1759/27720;
constant @cP = [\+] @P;

my @results;
@results[ @cP.first: { $_ > once rand }, :k ]++ xx TRIALS;

my @res;
say  'Event    Occurred Expected  Difference';
for ^@results {
    my ($occurred, $expected) = @results[$_], @P[$_] Ã— TRIALS;
    @res.push: sprintf "%-9s%8.0f%9.1f%12.1f",
            @event[$_],
                $occurred,
                     $expected,
                          abs $occurred - $expected;
}

.say for @res;

my $moar = qq:to/END/;
aleph      199689 200000.0       311.0
beth       166193 166666.7       473.7
gimel      143331 142857.1       473.9
daleth     125624 125000.0       624.0
he         110853 111111.1       258.1
waw         99829 100000.0       171.0
zayin       91015  90909.1       105.9
heth        63466  63456.0        10.0
END

my $jvm = qq:to/END/;
aleph      199748 200000.0       252.0
beth       166170 166666.7       496.7
gimel      143018 142857.1       160.9
daleth     124539 125000.0       461.0
he         111101 111111.1        10.1
waw        100587 100000.0       587.0
zayin       91185  90909.1       275.9
heth        63652  63456.0       196.0
END

use Test;
if $*VM ~~ /jvm/ {
is @res.join("\n"), chomp $jvm;
} else {
is @res.join("\n"), chomp $moar;
}
