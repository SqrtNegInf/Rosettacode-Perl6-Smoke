#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Playing_cards
#c# 2016-08-03 <>RC
#m# MOAR: OK
#j#  JVM: OK
#   made some new methods...

srand 314159;

enum Pip <A 2 3 4 5 6 7 8 9 10 J Q K>;
enum Suit<♦ ♣ ♥ ♠>;

class Card {
    has Pip $.pip;
    has Suit $.suit;

    method Str { $!pip ~ $!suit }
}

class Deck {
    has Card @.cards = pick *, map { Card.new(:$^pip, :$^suit) }, flat (Pip.pick(*) X Suit.pick(*));

    method shuffle { @!cards .= pick: * }

    method deal { shift @!cards }

    method cheat { splice @!cards, @!cards.elems-1, 1 }

    method peek { @!cards[0] }

    method Str  { ~@!cards }
    method gist { ~@!cards }
}

my Deck $d = Deck.new;
my @res.push: "Deck: $d";

my $top = $d.deal;
@res.push: "Deal top card: $top";

$d.shuffle;
@res.push: "Deck, re-shuffled: $d";

my $peek = $d.peek;
@res.push: "Peek at top card: $peek";

my $bottom = $d.cheat;
@res.push: "Bottom of deck card: $bottom";

@res.push: "Deck state now: $d";

.say for @res;

my $moar = qq:to/END/;
Deck: 3♥ 8♠ 10♣ Q♣ J♦ 3♠ 2♠ 5♣ Q♦ 2♥ 3♦ Q♥ 2♣ 6♥ 6♠ 9♣ 4♣ 9♦ A♥ K♣ K♦ 10♠ K♠ 4♥ 10♦ 4♦ A♣ 7♦ 8♦ 2♦ J♥ 5♦ 9♠ 10♥ 5♠ 5♥ 7♥ J♠ 4♠ A♦ 7♣ A♠ K♥ 8♥ 9♥ Q♠ 6♦ 8♣ 3♣ 7♠ J♣ 6♣
Deal top card: 3♥
Deck, re-shuffled: 4♣ 9♠ J♥ 2♠ 6♦ 4♦ K♦ K♥ A♣ 9♥ 8♥ 6♠ 6♣ A♦ 5♥ 4♥ 9♣ A♥ 2♣ Q♦ 5♠ 3♦ A♠ 10♥ 7♣ 3♠ Q♠ K♠ 5♣ 8♦ 3♣ J♠ Q♣ 2♦ J♣ K♣ 6♥ Q♥ 5♦ 7♥ J♦ 8♣ 4♠ 10♦ 8♠ 7♦ 10♠ 2♥ 7♠ 10♣ 9♦
Peek at top card: 4♣
Bottom of deck card: 9♦
Deck state now: 4♣ 9♠ J♥ 2♠ 6♦ 4♦ K♦ K♥ A♣ 9♥ 8♥ 6♠ 6♣ A♦ 5♥ 4♥ 9♣ A♥ 2♣ Q♦ 5♠ 3♦ A♠ 10♥ 7♣ 3♠ Q♠ K♠ 5♣ 8♦ 3♣ J♠ Q♣ 2♦ J♣ K♣ 6♥ Q♥ 5♦ 7♥ J♦ 8♣ 4♠ 10♦ 8♠ 7♦ 10♠ 2♥ 7♠ 10♣
END

my $jvm = qq:to/END/;
Deck: 7♥ 7♠ 10♣ 10♥ 6♣ K♥ 4♣ 2♣ J♦ 10♦ 4♠ 2♦ 5♣ 6♠ 10♠ 8♦ 2♥ K♦ J♥ 6♥ 5♠ 4♥ K♣ J♠ 8♥ 4♦ 7♦ 6♦ 5♥ Q♣ 3♣ 3♠ Q♦ Q♥ 3♥ 8♠ J♣ 2♠ 8♣ 9♠ 3♦ A♣ 9♥ A♥ 9♦ 5♦ K♠ 7♣ 9♣ A♦ A♠ Q♠
Deal top card: 7♥
Deck, re-shuffled: A♠ 9♥ J♥ 3♠ 6♦ 9♠ K♦ 4♥ 2♣ 5♥ 8♥ 6♣ 3♦ 8♦ A♥ A♣ 4♣ 5♦ 5♣ 7♦ 9♣ 3♥ J♣ 10♥ A♦ 5♠ Q♦ 6♥ 10♠ Q♥ K♠ 4♠ 10♣ 8♠ K♥ 9♦ Q♣ 4♦ J♦ 6♠ K♣ J♠ 2♥ 10♦ 3♣ 7♠ 2♦ 7♣ Q♠ 8♣ 2♠
Peek at top card: A♠
Bottom of deck card: 2♠
Deck state now: A♠ 9♥ J♥ 3♠ 6♦ 9♠ K♦ 4♥ 2♣ 5♥ 8♥ 6♣ 3♦ 8♦ A♥ A♣ 4♣ 5♦ 5♣ 7♦ 9♣ 3♥ J♣ 10♥ A♦ 5♠ Q♦ 6♥ 10♠ Q♥ K♠ 4♠ 10♣ 8♠ K♥ 9♦ Q♣ 4♦ J♦ 6♠ K♣ J♠ 2♥ 10♦ 3♣ 7♠ 2♦ 7♣ Q♠ 8♣
END

use Test;
if $*VM ~~ /jvm/ {
is @res.join("\n"), chomp $jvm;
} else {
is @res.join("\n"), chomp $moar;
}
