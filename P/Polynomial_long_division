#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Polynomial_long_division
#c# 2015-12-19 <>RC, 2018-11-23 >RC
#m# MOAR: OK
#j#  JVM: OK

# Works with: rakudo version 2015-09-30
#Translation of: Perl for the core algorithm; original code for LaTeX pretty-printing.
sub poly_long_div ( @n is copy, @d ) {
    return [0], |@n if +@n < +@d;

    my @q = gather while +@n >= +@d {
        @n = @n Z- flat ( ( @d XÃ— take ( @n[0] / @d[0] ) ), 0 xx * );
        @n.shift;
    }

    return @q, @n;
    #return $(@q), $(@n); # 'item contextualizer' not needed
}

sub xP ( $power ) { $power>1 ?? "x^$power" !! $power==1 ?? 'x' !! '' }
sub poly_print ( @c ) { join ' + ', @c.kv.map: { $^v ~ xP( @c.end - $^k ) } }

my @polys = [ [     1, -12, 0, -42 ], [    1, -3 ] ],
            [ [     1, -12, 0, -42 ], [ 1, 1, -3 ] ],
            [ [          1, 3,   2 ], [    1,  1 ] ],
            [ [ 1, -4,   6, 5,   3 ], [ 1, 2,  1 ] ];

my @res;
#say '<math>\begin{array}{rr}';
for @polys -> [ @a, @b ] {
    @res.push: poly_long_div( @a, @b ).map: { poly_print($_) };
    #printf Q"%s , &%s \\\\\n", poly_long_div( @a, @b ).map: { poly_print($_) };
}
#say '\end{array}</math>';
.say for @res;

my $ref = qq:to/END/;
1x^2 + -9x + -27 -123
1x + -13 16x + -81
1x + 2 0
1x^2 + -6x + 17 -23x + -14
END

use Test;
is @res.join("\n"), chomp $ref;
