#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Pseudo-random_numbers/Combined_recursive_generator_MRG32k3a
#c# 2020-08-16 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

class MRG32k3a {
    has @!x1;
    has @!x2;

    constant a1 = 0, 1403580, -810728;
    constant a2 = 527612, 0, -1370589;
    constant m1 = 2**32 - 209;
    constant m2 = 2**32 - 22853;

    submethod BUILD ( Int :$seed where 0 < * <= m1 = 1 ) { @!x1 = @!x2 = $seed, 0, 0 }

    method next-int {
        @!x1.unshift: (a1[0] * @!x1[0] + a1[1] * @!x1[1] + a1[2] * @!x1[2]) % m1; @!x1.pop;
        @!x2.unshift: (a2[0] * @!x2[0] + a2[1] * @!x2[1] + a2[2] * @!x2[2]) % m2; @!x2.pop;
        (@!x1[0] - @!x2[0]) % (m1 + 1)
    }

    method next-rat { self.next-int / (m1 + 1) }
}

# Test next-int with custom seed
@res.push: 'Seed: 1234567; first five Int values:';
my $rng = MRG32k3a.new :seed(1234567);
@res.push: $_ for $rng.next-int xx 5;

# Test next-rat (since these are rational numbers by default)
@res.push: "\nSeed: 987654321; first 1e5 Rat values histogram:";
$rng = MRG32k3a.new :seed(987654321);
@res.push: ( ($rng.next-rat * 5).floor xx 100_000 ).Bag.sort.subst: /\t/, ' ', :g;

# Test next-int with default seed
@res.push: "\nSeed: default; first five Int values:";
$rng = MRG32k3a.new;
@res.push: $_ for $rng.next-int xx 5;

.say for @res;

my $ref = q:to/END/;
Seed: 1234567; first five Int values:
1459213977
2827710106
4245671317
3877608661
2595287583

Seed: 987654321; first 1e5 Rat values histogram:
0 20002 1 20060 2 19948 3 20059 4 19931

Seed: default; first five Int values:
4294439476
798392476
1012402088
1268414424
3353586348
END

use Test;
is @res.join("\n"), chomp $ref;
