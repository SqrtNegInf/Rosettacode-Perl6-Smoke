#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Peaceful_chess_queen_armies
#c# 2019-11-xx >RC
#m# MOAR: OK
#j# JVM:  OK
#n# d'oh stop at first solution (was previously proceeding to the last before showing result)
#n# wart alert: in 'place' routine using literal '◦•' in regex because I cannot figure out a
#n# way to efficiently create a regex object which is built from $empty-square

# recursively place the next queen
sub place ($board, $n, $m, $empty-square) {
    state (%seen,$attack);
    state $solution = False;

    # logic of 'attack' regex: queen ( ... paths between queens containing only empty squares ... ) queen of other color
    once {
      my %Q = 'WBBW'.comb; # return the queen of alternate color
      my $re = 
        '(<[WB]>)' ~
        '[' ~
          join(' |', 
            qq/<[$empty-square]>*/, 
            map { 
              qq/ . ** {$_}[<[$empty-square]> . ** {$_}]*/ 
            }, $n-1, $n, $n+1
          ) ~
        ']' ~
        '<{%Q{$0}}>';
      $attack = "rx/$re/".EVAL;
    }

    # pass already-found result back up the stack
    return $solution if $solution;

    # bail out if seen this configuration previously, or attack detected
    return if %seen{$board}++ or $board ~~ $attack;

    # success if queen count is m×2, set state variable and return from recursion
    $solution = $board and return if $m * 2 == (my $queens = $board.comb.Bag{<W B>}.sum);

    # place the next queen (alternating colors each time)
    my $cnt = 0;
    place( $board.subst( /<[◦•]>/, {<W B>[$queens % 2]}, :nth($cnt) ), $n, $m, $empty-square )
        while $board ~~ m:nth(++$cnt)/<[◦•]>/;

    # 
    return $solution;
}

my $m = 4;
my $n = 5;

my $empty-square = '◦•';

my $board = ($empty-square x $n**2).comb.rotor($n)>>.join[^$n].join: "\n";

my $solution = place( $board, $n, $m, $empty-square );

say $solution 
    ?? "Solution to $m $n\n\n{S:g/(\N)/$0 / with $solution}"
    !! "No solution to $m $n";

my $ref = q:to/END/;
W•◦•W
•◦B◦•
◦B◦B◦
•◦B◦•
W•◦•W
END

use Test;
is $solution, chomp $ref;

#`{

W • ◦ • W
• ◦ B ◦ •
◦ B ◦ B ◦
• ◦ B ◦ •
W • ◦ • W

}
