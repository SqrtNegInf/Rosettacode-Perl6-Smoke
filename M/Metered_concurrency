#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Metered_concurrency
#c# 2016-06-04 <RC
#m# MOAR: OK
#j#  JVM: OK

#Uses a buffered channel to hand out a limited number of tickets.

class Semaphore {
    has $.tickets = Channel.new;
    method new ($max) {
        my $s = self.bless; 
        $s.tickets.send(True) xx $max;
        $s;
    }
    method acquire { $.tickets.receive }
    method release { $.tickets.send(True) }
}

sub MAIN ($units = 5, $max = 2) {
    my $a;
    my $r;
    my $sem = Semaphore.new($max);

    my @units = do for ^$units -> $u {
        start {
            $sem.acquire; say "unit $u acquired"; $a++;
            sleep 2;
            $sem.release; say "unit $u released"; $r++;
        }
    }
    await @units;

use Test;
ok $a == $r;
}

#`{
unit 0 acquired
unit 1 acquired
unit 0 released
unit 1 released
unit 3 acquired
unit 2 acquired
unit 3 released
unit 2 released
unit 4 acquired
unit 4 released
}
