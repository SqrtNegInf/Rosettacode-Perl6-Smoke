#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Word_search
#c# 2020-02-07 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# 2020-10-07 different answers when run from interactive shell vs cron-launch

srand 123456;

my @res;

my $rows = 10;
my $cols = 10;

my $message = q:to/END/;
    .....R....
    ......O...
    .......S..
    ........E.
    T........T
    .A........
    ..C.......
    ...O......
    ....D.....
    .....E....
    END

my %dir =
    '→' => (1,0),
    '↘' => (1,1),
    '↓' => (0,1),
    '↙' => (-1,1),
    '←' => (-1,0),
    '↖' => (-1,-1),
    '↑' => (0,-1),
    '↗' => (1,-1)
;

my @ws = $message.comb(/<print>/);

my $path = './ref/unixdict.txt'; # or wherever

my @words = $path.IO.slurp.words.grep( { $_ !~~ /<-[a..z]>/ and 2 < .chars < 11 } ).pick(*);
my %index;
my %used;

while @ws.first( * eq '.') {

    # find an unfilled cell
    my $i = @ws.grep( * eq '.', :k ).pick;

    # translate the index to x / y coordinates
    my ($x, $y) = $i % $cols, floor($i / $rows);

    # find a word that fits
    my $word = find($x, $y);

    # Meh, reached an impasse, easier to just throw it all
    # away and start over rather than trying to backtrack.
    restart, next unless $word;

    %used{"$word"}++;

    # Keeps trying to place an already used word, choices
    # must be limited, start over
    restart, next if %used{$word} > 15;

    # Already used this word, try again
    next if %index{$word.key};

    # Add word to used word index
    %index ,= $word;

    # place the word into the grid
    place($x, $y, $word);

}

display();

sub display {
    @res.push: flat "    ", 'ABCDEFGHIJ'.comb;
    @res.push: $_ for (^10).map: { ($_).fmt("  %2d"), @ws[$_ × $cols .. ($_ + 1) × $cols - 1] }
    @res.push: "\n  Words used:";
    my $max = 1 + %index.keys.max( *.chars ).chars;
    for %index.sort {
        @res.push: sprintf "%{$max}s %4s %s  ", .key, .value.key, .value.value;
        #print "\n" if $++ % 2;
    }
    @res.push: "\n"
}

sub restart {
    @ws = $message.comb(/<print>/);
    %index = ();
    %used = ();
}

sub place ($x is copy, $y is copy, $w) {
    my @word = $w.key.comb;
    my $dir  = %dir{$w.value.value};
    @ws[$y × $rows + $x] = @word.shift;
    while @word {
        ($x, $y) »+=« $dir;
        @ws[$y × $rows + $x] = @word.shift;
    }
 }

sub find ($x, $y) {
    my @trials = %dir.keys.sort.map: -> $dir { # DH this sort is used just to get stable results for testing
            my $space = '.';
            my ($c, $r) = $x, $y;
            loop {
                ($c, $r) »+=« %dir{$dir};
                last if 9 < $r|$c;
                last if 0 > $r|$c;
                my $l = @ws[$r × $rows + $c];
                last if $l ~~ /<:Lu>/;
                $space ~= $l;
            }
            next if $space.chars < 3;
            [$space.trans( '.' => ' ' ),
            ("{'ABCDEFGHIJ'.comb[$x]} {$y}" => $dir)]
        };

    for @words.pick(*) -> $word {
        for @trials -> $space {
            next if $word.chars > $space[0].chars;
            return ($word => $space[1]) if compare($space[0].comb, $word.comb)
        }
    }
}

sub compare (@s, @w) {
    for ^@w {
        next if @s[$_] eq ' ';
        return False if @s[$_] ne @w[$_]
    }
    True
}

.say for @res;

my $moar = q:to/END/;
     A B C D E F G H I J
   0 w t e i d R x g a y
   1 s t t f l y O e n s
   2 s u e o t u v S h e
   3 e p o e r r n a E q
   4 T i i i k r a n r T
   5 c A v l d s i m a y
   6 c y C h c u u d g a
   7 i b o O e s t y x m
   8 t b o s D y m s n i
   9 l l a b t E c r a m

  Words used:
      ami  J 6 ↓  
   annuli  I 5 ↖  
     ball  D 9 ←  
      bob  B 7 ↘  
     clip  E 6 ↖  
     cram  G 9 →  
     diet  E 0 ←  
      eng  J 2 ↖  
      fly  D 1 →  
      gay  H 0 →  
      gym  I 6 ↙  
      hex  I 2 ↖  
      hey  D 6 ↘  
      icc  A 7 ↑  
     mart  H 5 ↖  
      nyu  I 8 ↖  
      ram  H 9 →  
      seq  J 1 ↓  
    skeet  F 5 ↖  
      sob  D 8 ←  
      ssw  A 2 ↑  
 studious  H 8 ↖  
     styx  F 7 →  
      tic  A 8 ↑  
   torrid  C 1 ↘  
     vary  G 2 ↘  
      vie  C 5 ↖  
     yost  B 6 ↘  


END

my $moar-terminal = q:to/END/;
     A B C D E F G H I J
   0 r s s e t R k n i k
   1 i y d t l u O y i t
   2 p t v r u o h S h d
   3 e h e i o d t e E s
   4 T l o r e l f s j T
   5 t A v m t t d o a t
   6 c o C e o i y n v s
   7 o c o O n j a r a u
   8 m w i r D a s r u l
   9 b w o l b E l o y k

  Words used:
     blow  E 9 ←  
     comb  A 6 ↓  
      din  J 2 ↖  
     homo  B 3 ↘  
      hut  G 2 ↖  
      icc  C 8 ↖  
      ink  I 0 ←  
      ivy  D 3 ↖  
      jar  F 7 →  
     java  I 4 ↓  
     kink  J 0 ←  
 landlord  J 8 ↖  
     lore  B 4 →  
      low  D 9 ←  
      orr  H 9 ↑  
     ripe  A 0 ↓  
     root  D 8 ↖  
      say  G 8 ↑  
      shy  J 3 ↖  
      sse  B 0 →  
    stole  H 4 ↖  
     stud  C 0 ↘  
     sulk  J 6 ↓  
      tao  J 5 ←  
 tertiary  B 2 ↘  
    theft  J 1 ↙  
     ursa  I 8 ←  
    venal  C 5 ↘  
      woe  B 8 ↗  


END

my $jvm = q:to/END/;
     A B C D E F G H I J
   0 y s h e a R a n o v
   1 a e h r b s O p i y
   2 d n r s e u e S c l
   3 g e k b i m t k E e
   4 T y c l u n a r y T
   5 i A p i e a i r c l
   6 a h C d r a m m k n
   7 a g p O b e e v i z
   8 a e s u D y l a o d
   9 m l e h c E v c p f

  Words used:
      aaa  A 6 ↓  
    ankle  A 1 ↘  
   aubrey  F 5 ↖  
      bee  E 7 →  
   cleric  H 9 ↖  
      cpa  I 2 ↖  
      crt  I 5 ↖  
      cup  E 9 ↖  
      day  A 2 ↑  
 diminish  J 8 ↖  
     dram  D 6 →  
      ely  J 3 ↑  
      fpc  J 9 ←  
      gel  B 7 ↓  
      gyp  A 3 ↘  
     helm  D 9 ←  
   lunary  D 4 →  
    lykes  J 5 ↖  
      nov  H 0 →  
      phi  C 7 ↖  
      poi  I 9 ↑  
   remark  D 1 ↘  
      sea  C 8 ←  
     shea  B 0 →  
     tube  G 3 ↖  
     vain  G 9 ↗  
      viz  H 7 →  
      yea  F 8 ↑  
      yen  B 4 ↑  
      yip  J 1 ←  


END

use Test;
my $ref = $*VM ~~ /jvm/ ?? $jvm !! $moar;
is @res.join("\n"), chomp $ref;
