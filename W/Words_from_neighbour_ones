#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Words_from_neighbour_ones
#c# 2021-02-06 <RC
#m# MOAR: OK
#j# JVM:  OK

my @words_ge_9 = 'ref/unixdict.txt'.IO.lines.grep( *.chars >= 9 );
my %words_eq_9 =                 @words_ge_9.grep( *.chars == 9 ).Set;

my @new_words = gather for @words_ge_9.rotor( 9 => -8 ) -> @nine_words {
    my $new_word = [~] map { @nine_words[$_].substr($_, 1) }, ^9;

    take $new_word if %words_eq_9{$new_word};
}

#.say for unique @new_words;

my $ref = q:to/END/;
applicate
architect
astronomy
christine
christoph
committee
composite
constrict
construct
different
extensive
greenwood
implement
improvise
intercept
interpret
interrupt
philosoph
prescript
receptive
telephone
transcend
transport
transpose
END

use Test;
is (unique @new_words).join("\n"), chomp $ref;
